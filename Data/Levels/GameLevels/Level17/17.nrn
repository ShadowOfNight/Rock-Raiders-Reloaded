#include <nerpnrn.h>

TRUE ? SetTutorialFlags 0

Main()
{
	TRUE ? SetTutorialFlags 0
	TRUE ? SetMessagePermit 1

	// Check for completion
	GetCrystalsCurrentlyStored > 29 ? SetLevelCompleted
	// Check for failure
	GetOxygenLevel = 0 ? SetLevelFail

	// If no minifigs, and no toolstore, fail...
	GetMiniFiguresOnLevel > 0 ? :canrebuild
	GetToolStoresBuilt = 0 ? SetLevelFail
canrebuild:

	Function(DoNastyThings)
	{
		// Should Rock-Monsters emerge???
		// If they get too many crystals, make life difficult for them.
		GetCrystalsCurrentlyStored = 10 ? SetRockMonsterAtTutorial 1
		GetCrystalsCurrentlyStored = 15 ? SetRockMonsterAtTutorial 2
		GetCrystalsCurrentlyStored = 20 ? SetRockMonsterAtTutorial 3
		GetCrystalsCurrentlyStored = 21 ? SetRockMonsterAtTutorial 4
		GetCrystalsCurrentlyStored = 22 ? SetRockMonsterAtTutorial 3
		GetCrystalsCurrentlyStored = 23 ? SetRockMonsterAtTutorial 4
		// If they go a bit over the top with minifigures, put the frighteners up them!!!
		// If more than 9 men, randomly make slugs appear
		TRUE ? SetR1 0
		GetMinifiguresOnLevel > 9 ? AddR1 1
		GetRandom100 = 1 ? AddR1 1
		GetSlugsOnLevel = 0 ? AddR1 1
		// Make sure there are not too many already
		GetSlugsOnLevel > 5 ? :skip2
		// Change slug possibility depending upon crystal cound
		GetCrystalsCurrentlyStored < 10 ? :skip1
		TRUE ? AddR1 1
skip1:
		GetCrystalsCurrentlyStored < 25 ? :skip2
		TRUE ? AddR1 1
skip2:
		GetR1 > 3 ? GenerateSlug
		Stop
	}
	FuncEnd(DoNastyThings)
}
MainEnd()